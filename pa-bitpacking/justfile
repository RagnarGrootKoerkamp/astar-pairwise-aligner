# Run criterion
bench_json:
    cargo criterion --bench nw --message-format=json --plotting-backend=disabled -- --quiet --quick > bench.json
    jq -s '[.[] | select(.reason == "benchmark-complete") \
    | { \
        id: .id, \
        typical: .typical.estimate, \
        mean: .mean.estimate, \
        median: .median.estimate \
      }]' bench.json > bench_cleaned.json
    rm bench.json

build:
    cargo build -r --bench nw

bench bench='' *args='':
    cargo bench --bench nw -- --quiet -n "{{bench}}" {{args}}

# instructions per cycle
stat bench='' *args='': build
    perf stat cargo bench --bench nw -- --profile-time 5 "{{bench}}" {{args}}

# record time usage
record bench='' *args='': build
    perf record cargo bench --bench nw -- --profile-time 2 "{{bench}}" {{args}}
    perf report -n
report:
    perf report -n

# instructions per cycle
flamegraph bench='' *args='': build
    cargo flamegraph --bench nw -- --bench --profile-time 5 "{{bench}}" {{args}} && open flamegraph.svg

viz:
    cargo run -r -F vis

cpufreq:
    sudo cpupower frequency-set --governor performance -d 2.6GHz -u 2.6GHz

allow-profiling:
    sudo sh -c 'echo 1 >/proc/sys/kernel/perf_event_paranoid'

# 1. bench
# 2. stat
# 3. viz
