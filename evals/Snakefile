# See the makefile in the root of the repository for instructions.

rule all:
    input: ['table/tools.tsv', 'table/scaling_e.tsv', 'table/scaling_n.tsv']

import itertools
import shutil
import statistics

## LIMITS

# Timeout (cpu-time) in seconds, or 'unlimited'
TIMEOUT = 'unlimited'
# Memory limit (virtual memory) in KB, or 'unlimited'
MEMORY_LIMIT = 30000000

## TOOL DEFINITIONS
binaries = {
    'pa' : '../target/release/astar-pairwise-aligner',
    'edlib' : '../../edlib/meson-build/edlib-aligner',
    'wfa'   : '../../wfa2/bin/align_benchmark',
}
tool_command = {
    'sh':            binaries["pa"]      + ' -i {input} -o {stats_path} -ss -k {wildcards.k} -r {wildcards.r} --algorithm sh',
    'csh':           binaries["pa"]      + ' -i {input} -o {stats_path} -ss -k {wildcards.k} -r {wildcards.r} --algorithm csh',
    # Slower variants for comparison
    'sh-noprune':    binaries["pa"]      + ' -i {input} -o {stats_path} -ss -k {wildcards.k} -r {wildcards.r} --algorithm sh  --no-prune',
    'csh-noprune':   binaries["pa"]      + ' -i {input} -o {stats_path} -ss -k {wildcards.k} -r {wildcards.r} --algorithm csh --no-prune',
    'dijkstra':      binaries["pa"]      + ' -i {input} -o {stats_path} -ss                                   --algorithm dijkstra',
    # Others
    # We use a fork of edlib that takes the .seq files as input and processes pairs one by one.
    'edlib':         binaries["edlib"]   + ' -p -s {input}', # -p: compute the path, -s: no output
    'biwfa':         binaries["wfa" ]    + ' -i {input} -a edit-wfa --wfa-memory-mode ultralow',
}

# Command to generate testcases.
generate_command    = '../target/release/examples/generate_dataset -x {wildcards.cnt} -n {wildcards.n} -e {wildcards.e} {output}'

# Wrap `command` in a small shell script than handles timeouts and memory limits
# using `ulimit`.
# Writes the exit status into `status_path`.
def timeout(command, status_path):
    return f"""
# Disable coredumps and set the time and memory limit
ulimit -c 0
ulimit -t {TIMEOUT}
ulimit -v {MEMORY_LIMIT}
# Allow the command to fail.
set +e
{command}
status=$?
set -e
# Normal exit
if [ $status -eq 0 ] ; then
    echo ok > {status_path}
    exit 0
fi
# typically from SIGKILL (137) (when killed by linux) or SIGSEGV (139) or SIGABRT (134)
if [ $status -eq 137 ] || [ $status -eq 139 ] || [ $status -eq 134 ]; then
    echo limit {TIMEOUT} {MEMORY_LIMIT} $status > {status_path}
    exit 0
fi
echo Unexpected exit status $status
exit 1
"""

wildcard_constraints:
    # Error rates start with a digit.
    e="[0-9.]+",
    # Tool names do not start with a digit.
    tool='[^.0-9]*'

## WRAPPER CLASSES

# An input is a generated file containing test sequences.
class Input:
    # An input dataset is given by:
    def __init__(self, **kwargs):
        # The number of sequence pairs.
        self.cnt = int(kwargs['cnt'])
        # The length of each sequence.
        self.n = int(kwargs['n'])
        # The error rate.
        self.e = float(kwargs['e'])

    # The wildcard string used for pattern matches.
    def name_pattern():
        return 'x{cnt}-n{n}-e{e}'
    # The wildcard pattern for an input file.
    def pattern():
        return f'input/synthetic/{Input.name_pattern()}.seq'

    # The name of this input file.
    def name(self):
        return Path(f'x{self.cnt}-n{self.n}-e{self.e}')
    # The path of this input file.
    def path(self):
        return Path(f'input/synthetic/{self.name()}.seq')


# A run is an execution of a tool (with optional parameters) on an input.
# The most important part is the bench_path, which is where Snakemake will write the timings.
class Run:
    # A run consists of an input and tool, and optionally r and k parameters.
    def __init__(self, **kwargs):
        self.__dict__.update(kwargs)
        self.has_params = 'k' in kwargs and 'r' in kwargs
    # The wildcard pattern for an run benchmark with parameters.
    def pattern_with_params():
        return f'runs/{Input.name_pattern()}-A{{tool}}-r{{r}}-k{{k}}.bench'
    # The wildcard pattern for an run benchmark without parameters.
    def pattern():
        return f'runs/{Input.name_pattern()}-A{{tool}}.bench'
    # The benchmark path of this run.
    def bench_path(self):
        if self.has_params:
            return Path(f'runs/{self.input.name()}-A{self.tool}-r{self.r}-k{self.k}.bench')
        else:
            return Path(f'runs/{self.input.name()}-A{self.tool}.bench')
    # The status path of this run, where the exit status is written after success/timeout/out-of-memory.
    def status_path(self):
        return self.bench_path().with_suffix('.status')
    # The stats path of this run, where A*PA writes additional statistics.
    def stats_path(self):
        return self.bench_path().with_suffix('.stats')

# === GENERATE INPUT ===

rule generate_input:
    # Input generation has priority over other rules, so that all inputs are
    # generated at the start.
    priority: 2
    output: Input.pattern()
    shell: generate_command

# === RULES TO RUN ON TESTCASES ===

# Given a set of wildcards (input specs, tool, parameters), return the command to run.
def build_command(w):
    input = Input(**w)
    run = Run(input=input, **w)
    command = tool_command[w.tool].format(input=input.path(), e=input.e, wildcards=w, status_path=run.status_path(), stats_path=run.stats_path())
    return timeout(command, run.status_path())

# Benchmark the tool.
rule benchmark_tool:
    input: lambda w: Input(**w).path()
    benchmark: Run.pattern()
    params: command = lambda w: build_command(w)
    shell: '{params.command}'

# Benchmark the tool with given parameters.
rule benchmark_tool_with_params:
    input: lambda w: Input(**w).path()
    benchmark: Run.pattern_with_params()
    params: command = lambda w: build_command(w)
    shell: '{params.command}'

# === TABLE CONSTRUCTION ===

# Collect all run files into a single tsv:
# - input parameters
# - .bench file
# - .status file
# - .stats file if present
def make_table(runs, output):
    headers = ["alg","cnt","n","e","r","k","s","h:m:s","max_rss","max_vms","max_uss","max_pss","io_in","io_out","mean_load","cpu_time", "exit_status"]
    table_file = Path(output[0]).open('w')

    found_headers = False
    for run in runs:
        if run.stats_path().is_file():
            pa_headers = run.stats_path().read_text().splitlines()[0]
            table_file.write("\t".join(headers) + '\t' + pa_headers + '\n')
            found_headers = True
            break
    if not found_headers:
        table_file.write("\t".join(headers) + '\n')

    for run in runs:
        status = run.status_path().read_text().strip()
        try:
            stats = '\t' + run.stats_path().read_text().splitlines()[1]
        except:
            stats = ''
        for bench_line in run.bench_path().read_text().splitlines()[1:]:
            table_file.write(f'{run.tool}\t{run.input.cnt}\t{run.input.n}\t{run.input.e}\t{getattr(run, "r", "")}\t{getattr(run, "k", "")}\t{bench_line}\t{status}{stats}\n')

# === TOOL COMPARISON ===

# Returns the list of runs for the tool comparison table.
def tools_runs():
    N = 10**7
    ns = [3_000, 10_000, 30_000, 100_000, 300_000, 1_000_000, 3_000_000, 10_000_000]
    es = [0.01, 0.05, 0.10, 0.15]
    tools = ['edlib', 'biwfa', 'sh', 'csh']
    inputs = [Input(cnt=N//n, n=n, e=e) for n in ns for e in es if N//n > 0]
    return [
        (
            # NOTE: exact matches are used for e < 10%, and inexact matches for e >= 10%.
            Run(input=input,
                tool=tool,
                k=15,
                r=1 if input.e < 0.1 else 2)
            if tool in ['sh', 'csh'] else
            Run(input=input, tool=tool)
        )
        for input in inputs
        for tool in tools
    ]

rule tools_table:
    priority: 2
    input: lambda w: [run.bench_path() for run in tools_runs()]
    output: 'table/tools.tsv'
    run: make_table(tools_runs(), output)

# === SCALING WITH N ===

# Returns the list of runs for the scaling with n comparison with and without pruning.
def scaling_n_runs():
    ns = [100, 300, 1000, 3000, 10_000, 30_000, 100_000]
    e = 0.05
    r = 1
    k = 15
    # Split tools into two groups using a different N.
    fast_N = 10**7
    slow_N = 10**5
    fast_tools = ['sh', 'csh']
    slow_tools = ['sh-noprune', 'csh-noprune']
    fast_inputs = [Input(cnt=fast_N//n, n=n, e=e) for n in ns if fast_N//n > 0]
    slow_inputs = [Input(cnt=slow_N//n, n=n, e=e) for n in ns if slow_N//n > 0]
    # Dijkstra is separate because it does not use parameters.
    dijkstra_runs = [Run(input=input, tool=tool)
                        for input in slow_inputs
                        for tool in ['dijkstra']]
    fast_runs = [Run(input=input, tool=tool, r=r, k=k)
                        for input in fast_inputs
                        for tool in fast_tools]
    slow_runs = [Run(input=input, tool=tool, r=r, k=k)
                        for input in slow_inputs
                        for tool in slow_tools]
    return fast_runs + slow_runs + dijkstra_runs

rule scaling_n_table:
    priority: 2
    input: lambda w: [run.bench_path() for run in scaling_n_runs()]
    output: 'table/scaling_n.tsv'
    run: make_table(scaling_n_runs(), output)

# === SCALING WITH E ===

# Returns the list of runs for scaling with e, with exact and inexact matches.
def scaling_e_runs():
    N = 10**6
    n = 10000
    es = [0.01] + [x/100 for x in range(2, 35, 2)]
    rs = [1, 2]
    k = 9
    tools = ['sh', 'csh']

    inputs = [Input(cnt=N//n, n=n, e=e) for e in es if N//n > 0]
    # Dijkstra is separate because it does not use parameters.
    dijkstra_runs = [Run(input=input, tool=tool)
                        for input in inputs
                        for tool in ['dijkstra']]
    our_runs = [Run(input=input, tool=tool, r=r, k=k)
                        for r in rs
                        for input in inputs
                        for tool in tools]
    return dijkstra_runs + our_runs

rule scaling_e_table:
    priority: 2
    input: lambda w: [run.bench_path() for run in scaling_e_runs()]
    output: 'table/scaling_e.tsv'
    run: make_table(scaling_e_runs(), output)
